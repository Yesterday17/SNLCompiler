Program              -> ProgramHead DeclarePart ProgramBody

ProgramHead          -> PROGRAM ProgramName
ProgramName          -> IDENTIFIER

DeclarePart          -> TypeDec VarDec ProcDec

TypeDec              -> TypeDeclaration
TypeDec              -> ''
TypeDeclaration      -> TYPE TypeDecList
TypeDecList          -> TypeId EQ TypeName SEMICOLON TypeDecListMore
TypeDecListMore      -> TypeDecList
TypeDecListMore      -> ''
TypeId               -> IDENTIFIER
TypeName             -> BaseType
TypeName             -> StructureType
TypeName             -> IDENTIFIER
BaseType             -> INTEGER
BaseType             -> CHAR
StructureType        -> ArrayType
StructureType        -> RecordType
ArrayType            -> ARRAY SQBR_OPEN Low DOT2 Top SQBR_CLOSE OF BaseType
Low                  -> INTC
Top                  -> INTC
RecordType           -> RECORD FieldDecList END
FieldDecList         -> FieldDecType IdentifierList SEMICOLON FieldDecListMore
FieldDecListMore     -> FieldDecList
FieldDecListMore     -> ''
FieldDecType         -> BaseType
FieldDecType         -> ArrayType
IdentifierList       -> IDENTIFIER IdentifierListMore
IdentifierListMore   -> COMMA IDENTIFIER IdentifierListMore
IdentifierListMore   -> ''

VarDec               -> VarDeclaration
VarDec               -> ''
VarDeclaration       -> VAR VarDecList
VarDecList           -> TypeName IdentifierList SEMICOLON VarDecListMore
VarDecListMore       -> VarDecList
VarDecListMore       -> ''

ProcDec              -> ProcDeclaration
ProcDeclaration      -> PROCEDURE ProcName BR_OPEN ParamList? BR_CLOSE SEMICOLON ProcDecPart ProcBody ProcDeclaration
ProcDeclaration      -> ''
ProcName             -> IDENTIFIER
ParamList            -> Param ParamListMore
ParamList            -> ''
ParamListMore        -> SEMICOLON Param ParamListMore
ParamListMore        -> ''
Param                -> VAR TypeName IdentifierList
Param                -> TypeName IdentifierList
ProcDecPart          -> DeclarePart
ProcBody             -> ProgramBody

ProgramBody          -> BEGIN StatementList END
StatementList        -> Statement MoreStatement
MoreStatement        -> SEMICOLON Statement MoreStatement
MoreStatement        -> ''
Statement            -> ConditionalStatement
Statement            -> LoopStatement
Statement            -> InputStatement
Statement            -> OutputStatement
Statement            -> ReturnStatement
Statement            -> IDENTIFIER AssCall
AssCall              -> AssignmentRest
AssCall              -> CallStatementRest
AssignmentRest       -> VariableVisit ASSIGN Exp
ConditionalStatement -> IF RelExp THEN StatementList ELSE StatementList FI
LoopStatement        -> WHILE RelExp DO StatementList ENDWH
InputStatement       -> READ BR_OPEN IDENTIFIER BR_CLOSE
OutputStatement      -> WRITE BR_OPEN Exp BR_CLOSE
ReturnStatement      -> RETURN BR_OPEN Exp BR_CLOSE
CallStatementRest    -> BR_OPEN CallStatementRestExp BR_CLOSE
CallStatementRestExp -> Exp CommaExp
CallStatementRestExp -> ''
CommaExp             -> COMMA Exp CommaExp
CommaExp             -> ''
RelExp               -> Exp CmdOp Exp
Exp                  -> Term ExpPostFix
ExpPostFix           -> AddOp Exp
ExpPostFix           -> ''
Term                 -> Factor TermPostFix
TermPostFix          -> MultOp Term
TermPostFix          -> ''
Factor               -> BR_OPEN Exp BR_CLOSE
Factor               -> INTC
Factor               -> Variable
Variable             -> IDENTIFIER VariableVisit
VariableVisit        -> VariableVisitField VariableVisitIndex
VariableVisitField   -> DOT IDENTIFIER
VariableVisitField   -> ''
VariableVisitIndex   -> SQBR_OPEN Exp SQBR_CLOSE
VariableVisitIndex   -> ''
CmdOp                -> LT
CmdOp                -> EQ
AddOp                -> ADD
AddOp                -> MINUS
MultOp               -> MUL
MultOp               -> DIV